plugins {
    id 'java'
    alias(libs.plugins.openapiGenerator)
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation libs.openapiGenerator
    implementation libs.springDoc

}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/static/open-api.yaml".toString()
    outputDir = layout.buildDirectory.dir("generated").get().toString()
    apiPackage = "mx.com.segurossura.grouplife.openapi.api"
    invokerPackage = "mx.com.segurossura.grouplife.openapi.invoker"
    modelPackage = "mx.com.segurossura.grouplife.openapi.model"
    modelNameSuffix = "Dto"
    configOptions = [
            annotationLibrary    : "none",
            dateLibrary          : "java8",
            documentationProvider: "none",
            library              : "spring-boot",
            reactive             : "true",
            openApiNullable      : "false",
            useBeanValidation    : "true",
            useSpringBoot3       : "true",
            useSwaggerUI         : "true",
            interfaceOnly        : "true",
            serializableModel    : "true",
            modelBuilder         : "true"
    ]
    globalProperties = [
            "skipFormModel": "false"
    ]
}

openApiValidate {
    inputSpec.set("$projectDir/src/main/resources/static/open-api.yaml".toString())
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/src/main/java"
        }
    }
}